<!DOCTYPE html>
<html>
	<head>
		<title>Evolve An Image</title>
		<script type="text/coffeescript">
			$(document).ready ->


				imageDistance = (a, b) ->
					sum = 0
					for i in [0...Math.min a.length, b.length] by 1
						sum += Math.pow (a[i] - b[i]), 2
					Math.sqrt(sum)

				# These will be the 3 image datas that we track
				original = best = current = null
				stopped = false

				# The dimensions of the image we will learn
				width = height = 0

				# The canvas we will draw the current guess on
				canvas = $.synth("canvas.stop").appendTo("body")
				context = canvas.first().getContext('2d')
				context.clear = (c = 'black') ->
					@fillStyle = c
					@fillRect 0,0,width,height
				randomPoint = -> $ $.random.integer(0, width), $.random.integer(0,height)
				randomColor = -> "rgba(#{$.random.integer 0,256},#{$.random.integer 0,256},#{$.random.integer 0,256}, 0.5)"
				context.randomTriangle = ->
					offset = randomPoint().scale(.75)
					@beginPath()
					@moveTo (a = randomPoint().scale(.25).plus offset)...
					@lineTo randomPoint().scale(.25).plus(offset)...
					@lineTo randomPoint().scale(.25).plus(offset)...
					@lineTo a...
					@closePath()
					@fillStyle = randomColor()
					@fill()

				# Set up the original from a webcam source
				setupVideo = (cb) ->
					navigator.getUserMedia or= navigator.webkitGetUserMedia or navigator.mozGetUserMedia or navigator.msGetUserMedia
					onVideo = (stream) ->
						$.log "onVideo", stream
						video = $.synth("video[width=320][height=240]")
							.attr('src', URL.createObjectURL stream)
							.prependTo("body")
							.first()
						video.addEventListener 'loadedmetadata', (evt) ->
							$.log "on loadedmetadata", evt
							video.play()
							[width, height] = [320,240]
							canvas.attr { width, height }
							context = canvas.first().getContext('2d')
							$.delay 1000, ->
								$.log "Drawing to canvas", video
								video.pause()
								context.drawImage video, 0, 0, width, height
								cb null, video
					onError = (err) -> cb err, null
					navigator.getUserMedia { video: true }, onVideo, onError

				# Set up the original from an image of the mona lisa
				setupImage = (cb) ->
					$.Promise.image("monalisa.jpg").wait (err, image) ->
						return cb(err, null) if err?
						$("body").prepend image
						{width, height} = image
						canvas.attr { width, height }
						context.drawImage image, 0, 0, width, height
						cb null, image

				frame = ->
					context.randomTriangle()
					current = context.getImageData 0, 0, width, height
					current.dist = imageDistance current.data, original.data
					if current.dist < best.dist
						best = current
					context.putImageData best, 0, 0
					if best.dist > 100 and not stopped
						setTimeout frame, 0

				stageReady = ->
					$.log "Stage is ready."
					original = context.getImageData 0, 0, width, height
					context.clear('white')
					best = current = context.getImageData 0,0,width,height
					best.dist = imageDistance(best.data, original.data)
					$(".stop").click ->
						stopped = !stopped
						if not stopped
							setTimeout frame, 0
					setTimeout frame, 0

				do setupStage = ->
					setupVideo (err, video) ->
						$.log "setupVideo ->", err, video
						if err? then setupImage (err, image) ->
							$.log "setupImage ->", err, image
							unless err? then stageReady()
						stageReady()

		</script>
	</head>
	<body>
		<script src="./js/bling.js"></script>
		<script src="./js/coffee-script.js"></script>
	</body>
</html>

